buildscript {
    repositories {
        mavenLocal()
        maven {
            url "http://bdp.sany.com.cn/nexus/content/groups/public/"
        }
        maven {
            url "http://122.225.105.101:8081/nexus/content/groups/public/"
        }
    }
    dependencies {
        classpath 'org.akhikhl.gretty:gretty:+'
    }
}

configure(allprojects) { project ->
    group = PROJ_GROUP
    version = PROJ_VERSION
    apply plugin: "java"
    apply plugin: "maven-publish"
    apply plugin: "eclipse"
    apply plugin: "idea"
    apply plugin: "maven"
    eclipse {
        jdt {
            //if you want to alter the java versions (by default they are configured with gradle java plugin settings):
            sourceCompatibility = SOURCE_COMPILER_LEVEL
            targetCompatibility = TARGET_COMPILER_LEVEL
            javaRuntimeName = "../../org.eclipse.jdt.launching.JRE_CONTAINER"
        }
    }
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
    tasks.withType(Javadoc) {
        sourceCompatibility = SOURCE_COMPILER_LEVEL
        targetCompatibility = TARGET_COMPILER_LEVEL
        options.encoding = 'UTF-8'
    }
    dependencies {
        testCompile 'junit:junit:4.12'
    }
    if (project.getProperty('skipTest').equals("true")) {
        compileTestJava.enabled = false
        processTestResources.enabled = false
        testClasses.enabled = false
        test.enabled = false
    }
}

configure(subprojects - [project(":bboss-util"), project(":bboss-taglib"), project(":bboss-websocket")]) { subproject ->
    task sourcesJar(type: Jar) {
        classifier = "sources"
        from sourceSets.main.allJava
    }
    task javaDocJar(type: Jar, dependsOn: javadoc) {
        classifier = "javadoc"
        from javadoc.destinationDir
    }
    artifacts {
        archives sourcesJar, javaDocJar
    }
    repositories {
        mavenLocal()
        maven {
            url "http://bdp.sany.com.cn/nexus/content/groups/public/"
        }
        maven {
            url "http://122.225.105.101:8081/nexus/content/groups/public/"
        }
    }
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javaDocJar
            }
        }
        repositories {
            maven {
                url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                credentials {
                    username ""
                    password ""
                }
            }
            /**
             maven {url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
             credentials {username ""
             password ""}}*/
            mavenLocal()
        }
    }
}